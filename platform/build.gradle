apply plugin: 'com.android.application'

// 版本发布名字
def APP_NAME = 'aorise'
// 版本发布时间
def BUILD_TIME = new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))

//repositories {
//    flatDir {
//        dirs 'libs'
//    }
//}

/**
 * 1. gradle clean
 * 2. gradle build / gradle assembleRelease
 */
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "cn.aorise.platform"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.2"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            try {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file('../keystore/keyStore.jks')
                storePassword STORE_PASSWORD
                v2SigningEnabled false
            } catch (ex) {
                throw new InvalidUserDataException(ex.toString())
            }
        }
    }
    buildTypes {
        def BASE_URL = "BASE_URL"
        release {
            buildConfigField "String", BASE_URL, '"https://api.douban.com/"'
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "String", BASE_URL, '"https://api.douban.com/"'
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            //signingConfig signingConfigs.release
        }
    }

    // Rename APP
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def name = APP_NAME + '-' + variant.productFlavors[0].name
            def version = 'v' + defaultConfig.versionName

            println "variant.outputs: ${variant.buildType.name} output.outputFile: ${output.outputFile}"
            if (variant.buildType.name.equals('release')) {
                def apkName = name + '-' + version + '-' + BUILD_TIME + '-release.apk'
                output.outputFile = new File(output.outputFile.parent, apkName)
            }

            if (variant.buildType.name.equals('debug')) {
                def apkName = name + '-' + version + '-' + BUILD_TIME + '-debug.apk'
                output.outputFile = new File(output.outputFile.parent, apkName)
            }
        }
    }

    android.variantFilter { variant ->
        //减少build时间
        if (!variant.getFlavors().get(0).name.equals('sample')) {
            variant.setIgnore(true);
        }
    }

    productFlavors {
        sample {
            applicationId 'cn.aorise.sample'
            versionCode 1
            versionName "1.0.0"
            manifestPlaceholders = [PACKAGE_NAME_VALUE: "cn.aorise.sample",
                                    CLASS_NAME_VALUE  : "cn.aorise.sample.ui.activity.MainActivity"]
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // compile project(':commonLib')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    //    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.6-SNAPSHOT'
    //    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.6-SNAPSHOT'
    sampleCompile project(':plugin:sample')
    compile 'com.android.support:multidex:1.0.1'
}